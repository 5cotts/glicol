block = ${ SOI ~ ("\n"|WHITESPACE)* ~ ( (line|comment) ~ WHITESPACE* ~ ";"? ~ WHITESPACE* ~ ("\n" ~ WHITESPACE*)* )+ ~ EOI}
comment = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE* ~ !NEWLINE}
line = ${ reference ~ WHITESPACE+? ~ ":" ~ WHITESPACE+? ~ chain}
chain = ${ node ~ (WHITESPACE* ~ "\n"? ~ WHITESPACE* ~ ">>" ~ WHITESPACE+ ~ node)*  }
node = ${ name ~ WHITESPACE* ~ paras ~ (WHITESPACE+ ~ paras)* }
paras = ${  float | symbol | seq | reference | string }
float = ${ (("+" | "-")? ~ ASCII_DIGIT+) ~ "." ~ ASCII_DIGIT* ~ (^"e" ~ "-"? ~ ASCII_DIGIT+)? }
symbol = ${ "\\" ~ ASCII_ALPHANUMERIC* ~ "_"? ~ ASCII_DIGIT* }
seq = @{ compound ~ (WHITESPACE+ ~ compound)*}
compound = ${ note+ }
note = ${ midi | rest | reference }
string = ${ "`" ~ inner ~ "`" }
inner = @{ char* }
char = {
    !("`" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
reference = ${ ("~"|"_")? ~ ASCII_ALPHA_LOWER+ ~ ("_" | ASCII_ALPHA_LOWER | ASCII_DIGIT )* }
name = ${"bd"|"sn"|"hh"|"squsynth"|"trisynth"|"seq"|"speed"|"choose"|"mul"|"add"|
"linrange"|"apfdecay"|"delayn"|"delaymod"|
"sin"|"squ"|"imp"|"envperc"|"sampler"|"noiz"|"lpf"|"plate"|"onepole"|
"hpf"|"pha"|"buf"|"state"|"freeverb"|"pan"|"delay"|"apfgain"|"comb"|"mix"|"monosum"|
"const_sig"|"*"|"sp"|"spd"|"tri"|"noise"|"amplfo"|"balance"|"rlpf"|"rhpf"|"kick"|"ks"|
"pha"|"shape"|"sawsynth"|"saw"|"script"|"closure"| "r" }
rest = ${ "_" }
midi = ${ (ASCII_DIGIT)+ ~ !"." }
WHITESPACE = _{" "|","|"|"}

// name is now required!